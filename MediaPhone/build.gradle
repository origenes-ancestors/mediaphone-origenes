apply plugin: 'com.android.application'

android {
	compileSdkVersion 29

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone_ancestors']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		minSdkVersion 14
		targetSdkVersion 28
		versionCode 38
		versionName '1.6.1b' // numbering matches Com-Phone version; lettering is Origenes-specific updates
		// versionNameSuffix = '-beta'
		resConfigs 'es', 'en'
	}

	buildTypes {
		all {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt',
					'proguard-mediaphone_ancestors.txt'

			shrinkResources true

			ndk {
				abiFilters 'arm64-v8a', 'armeabi-v7a' // filter out Fresco's x86 and x86_64 (filesize is more important here)
			}
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.2.0'
	implementation 'androidx.exifinterface:exifinterface:1.2.0' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.3.1' // for FileProvider

	implementation 'com.facebook.fresco:fresco:2.1.0' // for ancestor image loading
	implementation 'com.squareup.retrofit2:retrofit:2.5.0' // for API queries
	implementation 'com.squareup.retrofit2:converter-gson:2.5.0' // for easier JSON parsing/serialisation
	implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0' // for debugging

	implementation 'com.google.code.findbugs:annotations:3.0.1', {
		// Need to exclude these, or build is broken by:
		//   com.android.dex.DexException: Multiple dex files define Ljavax/annotation/CheckForNull
		exclude module: 'jsr305'
		exclude module: 'jcip-annotations'
	}
}
